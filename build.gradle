buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.8.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("com.google.gms:google-services:4.4.4")
    }
}

plugins {
    id("com.vanniktech.maven.publish") version "0.30.0"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.vanniktech.maven.publish"

group = "com.entrig"
version = "0.0.2-dev"

android {
    namespace = "com.entrig.sdk"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 21
        targetSdk = 35
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")
        implementation("com.google.firebase:firebase-messaging:25.0.1")

        // Testing
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("junit:junit:4.13.2")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

mavenPublishing {
    publishToMavenCentral("CENTRAL_PORTAL")
    signAllPublications()

    coordinates("com.entrig", "entrig-android", "0.0.2-dev")

    pom {
        name = "Entrig Android SDK"
        description = "Push notifications for Supabase"
        url = "https://github.com/entrig/entrig-android"
        inceptionYear = "2025"

        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
            }
        }

        developers {
            developer {
                id = "entrig"
                name = "Entrig Team"
                email = "team@entrig.com"
            }
        }

        scm {
            url = "https://github.com/entrig/entrig-android"
            connection = "scm:git:git://github.com/entrig/entrig-android.git"
            developerConnection = "scm:git:ssh://git@github.com/entrig/entrig-android.git"
        }
    }
}
